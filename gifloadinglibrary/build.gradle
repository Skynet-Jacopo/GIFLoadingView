apply plugin: 'com.android.library'

apply plugin: 'com.jfrog.bintray'
// This is the library version used when deploying the artifact
version = "1.0.0"
android {
  compileSdkVersion 23
  buildToolsVersion "23.0.3"
  resourcePrefix "gifloadingview_" //这个随便填
  defaultConfig {
    minSdkVersion 16
    targetSdkVersion 23
    versionCode 1
    versionName version
    renderscriptTargetApi 19
    renderscriptSupportModeEnabled true
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'),
          'proguard-rules.pro'
    }
  }
}
dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])
  compile 'com.android.support:appcompat-v7:23.2.1'
  compile 'pl.droidsonroids.gif:android-gif-drawable:1.1.+'
  compile 'fr.tvbarthel.blurdialogfragment:lib:2.1.5'
}
def siteUrl = 'https://github.com/Rogero0o/GifLoadingView'
// 项目的主页
def gitUrl = 'https://github.com/Rogero0o/GifLoadingView.git'
// Git仓库的url
group = "com.roger.gifloadinglibrary" // Maven Group ID for the
// artifact，一般填你唯一的包名

task sourcesJar(type: Jar) {
  from android.sourceSets.main.java.srcDirs
  classifier = 'sources'
}
task javadoc(type: Javadoc) {
  source = android.sourceSets.main.java.srcDirs
  classpath +=
      project.files(android.getBootClasspath().join(File.pathSeparator))
}
task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}
artifacts {
  archives javadocJar
  archives sourcesJar
}
Properties properties = new Properties()
properties.load(
    project.rootProject.file('local.properties').newDataInputStream())
bintray {
  user = properties.getProperty("bintray.user")
  key = properties.getProperty("bintray.apikey")
  configurations = ['archives']
  pkg {
    repo = "maven"
    name = "GifLoadingLibrary" //发布到JCenter上的项目名字
    websiteUrl = siteUrl
    vcsUrl = gitUrl
    licenses = ["Apache-2.0"]
    publish = true
  }
}
